import NonFungibleToken from 0x1d7e57aa55817448 
import MetadataViews from 0x1d7e57aa55817448

pub contract BitSportNFT: NonFungibleToken {

    // Events
    //
    pub event ContractInitialized()
    pub event Withdraw(id: UInt64, from: Address?)
    pub event Deposit(id: UInt64, to: Address?)
    pub event Minted(id: UInt64, name: String)

    // Named Paths
    //
    pub let CollectionStoragePath: StoragePath
    pub let CollectionPublicPath: PublicPath
    pub let MinterStoragePath: StoragePath

    // totalSupply
    // The total number of BitSportNFT that have been minted
    //
    pub var totalSupply: UInt64
    
    access(self) var nameData: {String: String}

    // A BitSportNFT as an NFT
    //
    pub resource NFT: NonFungibleToken.INFT, MetadataViews.Resolver {

        pub let id: UInt64
        pub let description: String
        pub let asset_uri: String
        pub let name: String
        access(self) let attributes: {String: String}
        pub fun getAttributes(): {String: String} {
            return self.attributes
        }
        pub let symbol: String
        pub let royalty: Int
        pub let stats_uri: String
        pub let project_uri: String
        pub let serial_number: Int
        pub let collection: String


        init(
            id: UInt64, 
            description: String,
            asset_uri: String,
            name: String,
            attributes: {String: String},
            symbol: String,
            royalty: Int,
            stats_uri: String,
            project_uri: String,
            serial_number: Int,
            collection: String
            ) {
            self.id = id
            self.description = description
            self.asset_uri = asset_uri
            self.name = name
            self.attributes = attributes
            self.symbol = symbol
            self.royalty = royalty
            self.stats_uri = stats_uri
            self.project_uri = project_uri
            self.serial_number = serial_number
            self.collection = collection
        }

        pub fun getViews(): [Type] {
            return [
                Type<MetadataViews.Display>()
            ]
        }

        pub fun resolveView(_ view: Type): AnyStruct? {
            switch view {
                case Type<MetadataViews.Display>():
                    return MetadataViews.Display(
                        name: self.name,
                        description: self.description,
                        thumbnail: MetadataViews.IPFSFile(
                            cid: self.asset_uri, 
                            path: "sm.png"
                        )
                    )
            }

            return nil
        }
    }

    // This is the interface that users can cast their BitSportNFT Collection as
    // to allow others to deposit BitSportNFT into their Collection. It also allows for reading
    // the details of BitSportNFT in the Collection.
    pub resource interface BitSportNFTCollectionPublic {
        pub fun deposit(token: @NonFungibleToken.NFT)
        pub fun getIDs(): [UInt64]
        pub fun borrowNFT(id: UInt64): &NonFungibleToken.NFT
        pub fun borrowBitSportNFT(id: UInt64): &BitSportNFT.NFT? {
            // If the result isn't nil, the id of the returned reference
            // should be the same as the argument to the function
            post {
                (result == nil) || (result?.id == id):
                    "Cannot borrow BitSportNFT reference: The ID of the returned reference is incorrect"
            }
        }
    }

    // Collection
    // A collection of BitSportNFT NFTs owned by an account
    //
    pub resource Collection: BitSportNFTCollectionPublic, NonFungibleToken.Provider, NonFungibleToken.Receiver, NonFungibleToken.CollectionPublic {
        // dictionary of NFT conforming tokens
        // NFT is a resource type with an `UInt64` ID field
        //
        pub var ownedNFTs: @{UInt64: NonFungibleToken.NFT}

        // withdraw
        // Removes an NFT from the collection and moves it to the caller
        //
        pub fun withdraw(withdrawID: UInt64): @NonFungibleToken.NFT {
            let token <- self.ownedNFTs.remove(key: withdrawID) ?? panic("missing NFT")

            emit Withdraw(id: token.id, from: self.owner?.address)

            return <-token
        }

        // deposit
        // Takes a NFT and adds it to the collections dictionary
        // and adds the ID to the id array
        //
        pub fun deposit(token: @NonFungibleToken.NFT) {
            let token <- token as! @BitSportNFT.NFT

            let id: UInt64 = token.id

            // add the new token to the dictionary which removes the old one
            let oldToken <- self.ownedNFTs[id] <- token

            emit Deposit(id: id, to: self.owner?.address)

            destroy oldToken
        }

        // getIDs
        // Returns an array of the IDs that are in the collection
        //
        pub fun getIDs(): [UInt64] {
            return self.ownedNFTs.keys
        }

        // borrowNFT
        // Gets a reference to an NFT in the collection
        // so that the caller can read its metadata and call its methods
        //
        pub fun borrowNFT(id: UInt64): &NonFungibleToken.NFT {
            return &self.ownedNFTs[id] as &NonFungibleToken.NFT
        }

        // borrowBitSportNFT
        // Gets a reference to an NFT in the collection as a BitSportNFT,
        // exposing all of its fields (including the typeID & rarityID).
        // This is safe as there are no functions that can be called on the BitSportNFT.
        //
        pub fun borrowBitSportNFT(id: UInt64): &BitSportNFT.NFT? {
            if self.ownedNFTs[id] != nil {
                let ref = &self.ownedNFTs[id] as auth &NonFungibleToken.NFT
                return ref as! &BitSportNFT.NFT
            } else {
                return nil
            }
        }

        // destructor
        destroy() {
            destroy self.ownedNFTs
        }

        // initializer
        //
        init () {
            self.ownedNFTs <- {}
        }
    }

    // createEmptyCollection
    // public function that anyone can call to create a new empty collection
    //
    pub fun createEmptyCollection(): @NonFungibleToken.Collection {
        return <- create Collection()
    }

    // NFTMinter
    // Resource that an admin or something similar would own to be
    // able to mint new NFTs
    //
    pub resource NFTMinter {

        // mintNFT
        // Mints a new NFT with a new ID
        // and deposit it in the recipients collection using their collection reference
        //
        pub fun mintNFT(
            recipient: &{NonFungibleToken.CollectionPublic}, 
            description: String,
            asset_uri: String,
            name: String,
            attributes: {String: String},
            symbol: String,
            royalty: Int,
            stats_uri: String,
            project_uri: String,
            serial_number: Int,
            collection: String
        ) {
            pre {
                    BitSportNFT.nameData[name] == nil: "Cannot create NFT: Name Already Exists"
                }

            // deposit it in the recipient's account using their reference
            recipient.deposit(token: <-create BitSportNFT.NFT(
                id: BitSportNFT.totalSupply,
                description: description,
                asset_uri: asset_uri,
                name: name,
                attributes: attributes,
                symbol: symbol,
                royalty: royalty,
                stats_uri: stats_uri,
                project_uri: project_uri,
                serial_number: serial_number,
                collection: collection
            ))

            BitSportNFT.nameData[name] = name


            emit Minted(
                id: BitSportNFT.totalSupply,
                name: name
            )

            BitSportNFT.totalSupply = BitSportNFT.totalSupply + (1 as UInt64)
        }
    }

    // fetch
    // Get a reference to a BitSportNFT from an account's Collection, if available.
    // If an account does not have a BitSportNFT.Collection, panic.
    // If it has a collection but does not contain the itemID, return nil.
    // If it has a collection and that collection contains the itemID, return a reference to that.
    //
    pub fun fetch(_ from: Address, itemID: UInt64): &BitSportNFT.NFT? {
        let collection = getAccount(from)
            .getCapability(BitSportNFT.CollectionPublicPath)!
            .borrow<&BitSportNFT.Collection{BitSportNFT.BitSportNFTCollectionPublic}>()
            ?? panic("Couldn't get collection")
        // We trust BitSportNFT.Collection.borowBitSportNFT to get the correct itemID
        // (it checks it before returning it).
        return collection.borrowBitSportNFT(id: itemID)
    }

    // initializer
    //
    init() {
        // set rarity price mapping

        // Set our named paths
        self.CollectionStoragePath = /storage/BitSportNFTCollections
        self.CollectionPublicPath = /public/BitSportNFTCollections
        self.MinterStoragePath = /storage/BitSportNFTMinters

        // Initialize the total supply
        self.totalSupply = 0

        // Initialize the name data
        self.nameData = {}


        // Create a Minter resource and save it to storage
        let minter <- create NFTMinter()
        self.account.save(<-minter, to: self.MinterStoragePath)

        emit ContractInitialized()
    }
}
